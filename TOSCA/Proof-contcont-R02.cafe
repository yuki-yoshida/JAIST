require ProofContCont Proof-contcont.cafe

--> *********************************************************************************
--> (2) inv(S) and wfs(S) and cont(S) and not final(S) implies cont(SS) or final(SS).
--> *********************************************************************************

--> ***************************************************************
-->  ctrans [R02]:
-->     < (node(TND,IDND,created) SetND), SetCP, SetRQ, SetRL, MP >
-->  => < (node(TND,IDND,started) SetND), SetCP, SetRQ, SetRL, MP > 
-->     if allRQOfNDInStates(SetRQ,IDND,ready) .
--> ***************************************************************
select ProofContCont .
-- When R02 can be applied to the state S:
--> **************************************************************************
--> Step 2-1: Begin with the cases each of which matches to LHS of each rules.
--> **************************************************************************
:goal {
  eq contcont(< (node(tnd,idND,created) sND), sCP, sRQ, sRL, mp >) = true .
}
--> ***********************************************************
--> Step 2-2: Split the most general case for a rule into cases
-->  where the condition of the rule does or does not hold.
--> ***********************************************************
-- The condition of R02 does or does not hold for the node of idND.
:ctf {
  eq allRQOfNDInStates(sRQ,idND,ready) = true .
}
-- Case 1: When the condition of R02 holds for the node of idND:
--> ***************************************************************
--> Step 2-3: Split the rule applied case into cases
-->  where predicate final does or does not hold in the next state.
--> ***************************************************************
-- All of the other nodes are or are not started.
:csp {
  eq allNDInStates(sND,started) = true .
  eq sND = (node(tnd',idND',initial) sND') .
  eq sND = (node(tnd',idND',created) sND') .
}
-- Case 1-1: When all of the other nodes are started:
:apply (rd) -- 1-1
-- Case 1-2: When there is an initial node:
:apply (rd) -- 1-2
-- Case 1-3: When there is a created node:
:apply (rd) -- 1-3
-- Case 2: When the condition of R02 does not hold for the node of idND:
:apply (rd) -- 2
show proof
select
